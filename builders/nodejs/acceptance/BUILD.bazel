load("//internal/acceptance:defs.bzl", "acceptance_test_argo_source", "acceptance_test_suite")
load(":args.bzl", "nodejsargs")
load("runtime.bzl", "gae_runtimes", "gcf_runtimes")

licenses(["notice"])

exports_files([
    "config.yaml",
    "args.bzl",
])

test_suite(
    name = "acceptance_test",
    tests = [
        gcf_runtimes["nodejs8"] + "_gcf_test",
        "flex_test",
        "gae_test",
        "gcf_test",
        "gcp_test",
    ],
)

VERSIONS_ALL = gae_runtimes

# nodejs8 is decommissioned for GAE
VERSIONS_WITH_GAE_SUPPORT = {n: v for n, v in gae_runtimes.items() if not v.startswith("8")}

# nodejs8 will be decommissioned for GCF on June 5, 2023
VERSIONS_WITH_GCF_SUPPORT = {n: v for n, v in gcf_runtimes.items() if not v.startswith("8")}

VERSIONS_WITH_FLEX_SUPPORT = {n: v for n, v in gcf_runtimes.items() if (n.startswith("nodejs18") or n.startswith("nodejs20"))}

# The GCF Legacy Worker is only available and used for the "GCF nodejs8" runtime version. It has
# different behavior, a unique order group, and is tested seperately.
VERSION_GCF_LEGACY_WORKER = gcf_runtimes["nodejs8"]

VERSIONS_WITH_GCP_SUPPORT = VERSIONS_ALL

acceptance_test_suite(
    name = "gcp_test",
    srcs = [
        "gcp_test.go",
    ],
    builder = "//builders/nodejs:builder.tar",
    rundir = ".",
    testdata = "//builders/testdata/nodejs:generic",
    versions = VERSIONS_WITH_GCP_SUPPORT,
)

# Test the run image tagged as latest in gae-runtimes.
acceptance_test_suite(
    name = "gae_test",
    srcs = [
        "gae_test.go",
    ],
    argsmap = nodejsargs(),
    builder = "//builders/nodejs:builder.tar",
    rundir = ".",
    testdata = "//builders/testdata/nodejs:appengine",
    versions = VERSIONS_WITH_GAE_SUPPORT,
)

# Test the run image tagged as ${_CANDIDATE_NAME} in gae-runtimes-private.
acceptance_test_suite(
    name = "gae_candidate_test",
    srcs = [
        "gae_test.go",
    ],
    argsmap = nodejsargs("$${_CANDIDATE_NAME}"),
    builder = "//builders/nodejs:builder.tar",
    rundir = ".",
    testdata = "//builders/testdata/nodejs:appengine",
    versions = VERSIONS_WITH_GAE_SUPPORT,
)

acceptance_test_suite(
    name = "gcf_test",
    srcs = [
        "gcf_test.go",
    ],
    builder = "//builders/nodejs:builder.tar",
    rundir = ".",
    testdata = "//builders/testdata/nodejs:functions",
    versions = VERSIONS_WITH_GCF_SUPPORT,
)

acceptance_test_suite(
    name = "8.17.0_gcf_test",
    srcs = [
        "legacy_worker_gcf_test.go",
    ],
    args = [
        "-runtime-version=" + VERSION_GCF_LEGACY_WORKER,
    ],
    builder = "//builders/nodejs:builder.tar",
    rundir = ".",
    testdata = "//builders/testdata/nodejs:functions",
)

acceptance_test_argo_source(
    name = "argo_source",
    srcs = [
        ":8.17.0_gcf_test_cloudbuild_bin",
    ],
    testdata = "//builders/testdata:nodejs",
)

acceptance_test_suite(
    name = "flex_test",
    srcs = ["flex_test.go"],
    builder = "//builders/nodejs:builder.tar",
    rundir = ".",
    testdata = "//builders/testdata/nodejs:flex",
    versions = VERSIONS_WITH_FLEX_SUPPORT,
)

exports_files(["runtime.bzl"])
